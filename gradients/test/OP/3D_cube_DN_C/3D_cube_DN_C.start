! units [F]=N, [L]=mm, [t]=s
! 2D Cantilever, plane stress with thickness

FINISH		    ! closing the current module
/CLEAR,NOSTART	! clear the database

pb_name='3D_cube_DN-C'
/FILNAME, pb_name, 1
!-------------------------------------------------------------------------------
!PARAMETERS
!Geometric parameters macro scale CORE (2D case)
a_1=200 			  	  ! Size along x [mm]
a_2=200				  ! Size along y [mm]
a_3=200   			      ! Plate thickness [mm]


!Material Parameters
C11=1.0
C12=0.4286
C13=0.4286
C14=0
C15=0
C16=0
C22=1.0
C23=0.4286
C24=0
C25=0
C26=0
C33=1.0
C34=0
C35=0
C36=0
C44=0.2857
C45=0
C46=0
C55=0.2857
C56=0
C66=0.2857
dens_al=1.0   ! Density [kg/mm3]

!Mesh parameters
!e_size=8
n_div=30

!Load parameters
F_z=1 	    !applied force [N]
delta_x=1	!applied displacement [mm]
delta_y=1	!applied displacement

!SIMP Parameter
p_c=3

/PREP7
/nopr
!GEOMETRY
BLOCK,0,a_1,0,a_2,0,a_3
!-------------------------------------------------------------------------------
!ELEMENT SELECTION AND MATERIAL
ET,1,SOLID185   


!MATERIAL
TB,ANEL,1,1,21,0
TBTEMP,0
TBDATA,,C11,C12,C13,C14,C15,C16 
TBDATA,,C22,C23,C24,C25,C26,C33 
TBDATA,,C34,C35,C36,C44,C45,C46 
TBDATA,,C55,C56,C66,,,


!-------------------------------------------------------------------------------
! Mesh attributes
ESIZE,,n_div

! Mesh
VSEL,all
VATT,1, ,1,0,
MSHKEY,1
VMESH,ALL

ALLSEL,ALL

*GET,E_N,ELEM,,COUNT
*DIM,EL_ID, array,E_N,1
*VGET,EL_ID(1),ELEM,,ELIST !returns the elements list

*DIM,VOLUME_E, array,E_N,1
*DIM,GAMMA_E, array,E_N,1
*DIM,XG, array,E_N,1
*DIM,YG, array,E_N,1
*DIM,ZG, array,E_N,1
*DIM,CONN_1, array,E_N,1
*DIM,CONN_2, array,E_N,1
*DIM,CONN_3, array,E_N,1
*DIM,CONN_4, array,E_N,1
*DIM,CONN_5, array,E_N,1
*DIM,CONN_6, array,E_N,1
*DIM,CONN_7, array,E_N,1
*DIM,CONN_8, array,E_N,1

*DO,i,1,E_N
	*GET,aux,ELEM,EL_ID(i),VOLU
	*GET,mat_curr,ELEM,EL_ID(i),ATTR,MAT
	*GET,dens_curr,DENS,mat_curr,
	*GET,XG_curr,ELEM,EL_ID(i),CENT,X
	*GET,YG_curr,ELEM,EL_ID(i),CENT,y	
	*GET,ZG_curr,ELEM,EL_ID(i),CENT,z
	*GET,CONN_1_curr,ELEM,EL_ID(i),NODE,1
	*GET,CONN_2_curr,ELEM,EL_ID(i),NODE,2
	*GET,CONN_3_curr,ELEM,EL_ID(i),NODE,3
	*GET,CONN_4_curr,ELEM,EL_ID(i),NODE,4
	*GET,CONN_5_curr,ELEM,EL_ID(i),NODE,5
	*GET,CONN_6_curr,ELEM,EL_ID(i),NODE,6
	*GET,CONN_7_curr,ELEM,EL_ID(i),NODE,7
	*GET,CONN_8_curr,ELEM,EL_ID(i),NODE,8
	VOLUME_E(i)=aux
	XG(i)=XG_curr
	YG(i)=YG_curr
	ZG(i)=ZG_curr
	CONN_1(i)=CONN_1_curr
	CONN_2(i)=CONN_2_curr
	CONN_3(i)=CONN_3_curr
	CONN_4(i)=CONN_4_curr
	CONN_5(i)=CONN_5_curr
	CONN_6(i)=CONN_6_curr
	CONN_7(i)=CONN_7_curr
	CONN_8(i)=CONN_8_curr
	GAMMA_E(i)=dens_curr
*ENDDO

! NODE Values
!NSEL,ALL
!*VGET,NODE_X,NODE,,LOC,X
!*VGET,NODE_Y,NODE,,LOC,Y
!*VGET,NODE_Z,NODE,,LOC,Z

ALLSEL,ALL

!-------------------------------------------------------------------------------
!Writing the output file
*CFOPEN,output_data_ELEM,txt
*VWRITE,EL_ID(1),CONN_1(1),CONN_2(1),CONN_3(1),CONN_4(1),CONN_5(1),CONN_6(1),CONN_7(1),CONN_8(1),XG(1),YG(1),ZG(1),VOLUME_E(1),GAMMA_E(1)
(14E24.15)
*CFCLOS

!component creation
nsel,s,loc,z,a_3
nsel,r,loc,y,a_2/2  
nsel,r,loc,x,a_1/2
cm,n_force,node


nsel,s,loc,z,a_3
nsel,r,loc,y,0  
nsel,r,loc,x,a_1
cm,n_dx,node
nsel,s,loc,z,a_3
nsel,r,loc,y,a_2  
nsel,r,loc,x,0
cm,n_mdx,node


nsel,s,loc,z,a_3
nsel,r,loc,y,a_2  
nsel,r,loc,x,a_1
cm,n_dy,node
nsel,s,loc,z,a_3
nsel,r,loc,y,0  
nsel,r,loc,x,0
cm,n_mdy,node

nsel,s,loc,z,0
nsel,r,loc,x,0
nsel,r,loc,y,0,
cm,n_fix1,node
nsel,s,loc,z,0
nsel,r,loc,x,a_1
nsel,r,loc,y,0
cm,n_fix2,node
nsel,s,loc,z,0
nsel,r,loc,x,0
nsel,r,loc,y,a_2,
cm,n_fix3,node
nsel,s,loc,z,0
nsel,r,loc,x,a_1
nsel,r,loc,y,a_2
cm,n_fix4,node

CMGRP, n_fix, n_fix1, n_fix2, n_fix3, n_fix4,

ALLSEL,ALL

FINISH $  /EXIT, all
